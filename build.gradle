import org.flywaydb.gradle.task.FlywayMigrateTask

buildscript {
  ext {
    springBootVersion = "2.0.6.RELEASE"
  }

  repositories {
    mavenCentral()
  }

  dependencies {
    classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
  }
}

plugins {
  id "java"
  id "org.flywaydb.flyway" version "5.2.4"
}

apply plugin: 'org.springframework.boot'

repositories {
  mavenCentral()
}

dependencies {
  compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.0'
  compileOnly 'org.projectlombok:lombok:1.18.8'
  annotationProcessor 'org.projectlombok:lombok:1.18.8'

  compile("org.springframework.boot:spring-boot-starter-web:$springBootVersion")
  compile("org.springframework.boot:spring-boot-starter-jdbc:$springBootVersion")

  compile("mysql:mysql-connector-java:8.0.12")

  testCompile("org.springframework.boot:spring-boot-starter-test:$springBootVersion")
  testCompile("org.mockito:mockito-core:2.23.4")
}

def developmentDbUrl = "jdbc:mysql://localhost:3306/tracker_dev?user=tracker&useSSL=false&useTimezone=true&serverTimezone=UTC&useLegacyDatetimeCode=false"
def mysqlUser = "tracker"
bootRun.environment([
        "WELCOME_MESSAGE": "hello",
        "SPRING_DATASOURCE_URL": developmentDbUrl,
        "MYSQL_USER": mysqlUser
])

def testDbUrl = "jdbc:mysql://localhost:3306/tracker_test?user=tracker&useSSL=false&useTimezone=true&serverTimezone=UTC&useLegacyDatetimeCode=false"
test.environment([
        "WELCOME_MESSAGE": "Hello from test",
        "SPRING_DATASOURCE_URL": testDbUrl,
        "MYSQL_USER": mysqlUser
])

flyway {
  url = developmentDbUrl
  user = mysqlUser
  password = ""
  locations = ["filesystem:databases/tracker/migrations"]
}

task testMigrate(type: FlywayMigrateTask) {
  url = testDbUrl
}